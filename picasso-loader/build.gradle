plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "com.automattic.android.publish-to-s3"
}

android {
    namespace "org.wordpress.aztec.picassoloader"

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation aztecProjectDependency

    implementation 'androidx.collection:collection:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'

    implementation "com.squareup.picasso:picasso:$picassoVersion"
}

dependencyAnalysis {
    issues {
        onUnusedDependencies {
            // This dependency is actually needed otherwise the build fails on 'aztec.tag'.
            exclude("androidx.appcompat:appcompat")
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            PicassoLoaderPublication(MavenPublication) {
                from components.release

                groupId "org.wordpress.aztec"
                artifactId "picasso-loader"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}
