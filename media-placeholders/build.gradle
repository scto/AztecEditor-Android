plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "com.automattic.android.publish-to-s3"
}

android {
    namespace "org.wordpress.aztec.placeholders"

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = androidxComposeCompilerVersion
    }
}

dependencies {
    implementation aztecProjectDependency
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    implementation "androidx.compose.foundation:foundation-layout:$androidxComposeLibraryVersion"
    implementation "androidx.compose.ui:ui:$androidxComposeLibraryVersion"
    implementation "androidx.compose.ui:ui-unit:$androidxComposeLibraryVersion"
}

dependencyAnalysis {
    issues {
        onUnusedDependencies {
            // This dependency is actually needed otherwise the build fails in multiple places.
            exclude("androidx.appcompat:appcompat")
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            WordPressShortcodesPublication(MavenPublication) {
                from components.release

                groupId "org.wordpress.aztec"
                artifactId "media-placeholders"
                // version is set by 'publish-to-s3' plugin
            }
        }
    }
}
